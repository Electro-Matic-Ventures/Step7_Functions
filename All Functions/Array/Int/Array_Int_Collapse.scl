FUNCTION "Array: Int: Collapse" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      null_value : Int;   // value that indicates null data
   END_VAR

   VAR_IN_OUT 
      int_array : Variant;
   END_VAR

   VAR_TEMP 
      i : Int;
      j : Int;
      len_int : Int;
      len_udint : UDInt;
      status : Int;
      t_int_array : Array[0..999] of Int;
   END_VAR


BEGIN
	REGION "COPY TO LOCAL MEMORY"
	    #len_udint := CountOfElements(#int_array);
	    #len_int := UDINT_TO_INT(#len_udint) - 1;
	    #status := MOVE_BLK_VARIANT(SRC := #int_array,
	                                COUNT := #len_udint,
	                                SRC_INDEX := 0,
	                                DEST_INDEX := 0,
	                                DEST => #t_int_array);
	END_REGION
	
	REGION "COLLAPSE IN PLACE"
	    FOR #i := 0 TO #len_int - 1 DO
	        IF #t_int_array[#i] = #null_value THEN
	            FOR #j := #i + 1 TO #len_int DO
	                IF #t_int_array[#j] <> #null_value THEN
	                    #t_int_array[#i] := #t_int_array[#j];
	                    #t_int_array[#j] := #null_value;
	                    EXIT;
	                END_IF;
	            END_FOR;
	        END_IF;
	    END_FOR;
	END_REGION
	
	REGION "RETURN"
	    #status := MOVE_BLK_VARIANT(SRC := #t_int_array,
	                                COUNT := #len_udint,
	                                SRC_INDEX := 0,
	                                DEST_INDEX := 0,
	                                DEST => #int_array);
	END_REGION
	
END_FUNCTION

