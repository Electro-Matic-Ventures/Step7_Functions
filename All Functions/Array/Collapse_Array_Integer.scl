FUNCTION "Collapse Array: Int" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      last_entry_index : Int;   // index of last piece of data
      null_value : Int;   // value that indicates null data
   END_VAR

   VAR_IN_OUT 
      int_array : Variant;
   END_VAR

   VAR_TEMP 
      t_int_array : Array[0..999] of Int;
      status : Int;
      length_int_array : UDInt;
      i : Int;
      j : Int;
   END_VAR


BEGIN
	REGION "COPY TO LOCAL MEMORY"
	    #length_int_array := CountOfElements(#int_array);
	    #status := MOVE_BLK_VARIANT(SRC := #int_array,
	                                COUNT := #length_int_array,
	                                SRC_INDEX := 0,
	                                DEST_INDEX := 0,
	                                DEST => #t_int_array);
	END_REGION
	
	REGION "COLLAPSE IN PLACE"
	    FOR #i := 0 TO #last_entry_index DO
	        IF #t_int_array[#i] = #null_value THEN
	            FOR #j := #i + 1 TO #last_entry_index DO
	                IF #t_int_array[#j] <> #null_value THEN
	                    #t_int_array[#i] := #t_int_array[#j];
	                    #t_int_array[#j] := #null_value;
	                    EXIT;
	                END_IF;
	            END_FOR;
	        END_IF;
	    END_FOR;
	END_REGION
	
	REGION "RETURN"
	    #status := MOVE_BLK_VARIANT(SRC := #t_int_array,
	                                COUNT := #length_int_array,
	                                SRC_INDEX := 0,
	                                DEST_INDEX := 0,
	                                DEST => #int_array);
	END_REGION
	
END_FUNCTION

